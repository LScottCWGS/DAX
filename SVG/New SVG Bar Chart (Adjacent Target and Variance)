New SVG Bar Chart (Adjacent Target and Variance) = 
-- SVG measure
-- Use this inside of a Table or a Matrix visual.
-- The 'Image size' property of the Table or Matrix must match the values in the config below


----------------------------------------------------------------------------------------
-------------------- START CONFIG - SAFELY CHANGE STUFF IN THIS AREA -------------------
----------------------------------------------------------------------------------------


-- Input field config
VAR _Actual = [# Total Quantity]
VAR _Target = [# Max Value in Months]
VAR _Performance = DIVIDE ( _Actual - _Target, _Target )


-- Chart Config
VAR _BarMax = 100
VAR _BarMin = 0
VAR _Scope = ALLSELECTED ( 'DateKey'[Month-Year] )


-- Color config.
VAR _ActualColor        = "#686868" -- Charcoal
VAR _TargetColor        = "#e1dfdd" -- Grey 
VAR _VarianceColor     = 
    IF ( 
        _Performance < 0, 
        "#fab005",  -- Yellow
        "#2094ff"   -- Blue
    )


----------------------------------------------------------------------------------------
----------------------- END CONFIG - BEYOND HERE THERE BE DRAGONS ----------------------
----------------------------------------------------------------------------------------


VAR _MaxActualsInScope = 
    CALCULATE(
        MAXX(
            _Scope,
            [# Total Quantity]
        ),
        REMOVEFILTERS( 'DateKey'[Year] )
    )

VAR _MaxTargetInScope = 
    CALCULATE(
        MAXX(
            _Scope,
            [# Max Value in Months]
        ),
        REMOVEFILTERS( 'DateKey'[Year] )
    )

VAR _AxisMax = 
    IF (
        HASONEVALUE ( 'DateKey'[Year] ),
        MAX( _MaxActualsInScope, _MaxTargetInScope ),
        CALCULATE( MAX( [# Total Quantity], [# Max Value in Months] ), REMOVEFILTERS( 'DateKey'[Year] ) )
    ) * 1.1


-- Normalize values (to get position along X-axis)
    VAR _AxisRange = 
        _BarMax - _BarMin
    
    VAR _ActualNormalized = 
        DIVIDE ( _Actual, _AxisMax ) * _AxisRange

    VAR _TargetNormalized = 
        DIVIDE ( _Target, _AxisMax ) * _AxisRange


-- Vectors and SVG code
VAR _SvgPrefix = "data:image/svg+xml;utf8, <svg xmlns='http://www.w3.org/2000/svg'>"

VAR _Sort = "<desc>" & FORMAT ( _Actual, "000000000000" ) & "</desc>"

VAR _ActualBar     = "<rect x='" & _BarMin & "' y='4' width='" & _ActualNormalized & "' height='8' fill='" & _ActualColor & "'/>"
VAR _TargetBar     = "<rect x='" & _BarMin & "' y='13' width='" & _TargetNormalized & "' height='8' fill='" & _TargetColor & "'/>"
VAR _VarianceBar   = "<rect x='" & _BarMin + MIN( _ActualNormalized, _TargetNormalized ) & "' y='" & IF ( _Target > _Actual, 4, 13 ) & "' width='" & ABS( _ActualNormalized - _TargetNormalized ) & "' height='8' fill='" & _VarianceColor & "'/>"

VAR _SvgSuffix = "</svg>"


-- Final result
VAR _SVG = 
    _SvgPrefix 
    
    & _Sort

    & _TargetBar
    & _ActualBar 
    & _VarianceBar

    & _SvgSuffix

RETURN
    _SVG
